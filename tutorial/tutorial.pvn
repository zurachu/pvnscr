PVSC	;ヘッダ

;←ここから行末までコメント
;============================
;|P/ECE VISUALNOVEL Scripter|
;| チュートリアルスクリプト |
;|  (c)2004 てとら★ぽっと  |
;============================

savefile "pvnscr_t.sav"	;セーブファイルを使う場合最初に定義すること
setwin 1,3,1,1

bg 3

gosub *点々

bg "bg_sky"
ld "pvc_rv",1
snd "ohit",1

br
主役の座はいただいた！！（挨拶\

sndstop 1
bgm "winnie"

Ｐ／ＶＮＳｃｒｉｐｔｅｒのチュートリアルへようこそ〜♪\
司会のラビルっていいます。@
ど〜ぞよろしく。\
このチュートリアルでは、Ｐ／ＶＮＳｃｒｉｐｔｅｒでノベルを作る方法を紹介していきま〜す。

if $0=1 goto *分岐	;最後まで見たフラグが立っていれば自由に選択できる

;=======================================
*message	;ラベル

!d100

まずはメッセージ表示から。\

*message1
setwin 1,3,1,1

メッセージはスクリプト中に全角で書いた文字がそのまま表示されます。\
途中でＡボタンを待ちたい場合は＠（アットマーク）。@
ページ送りは￥。\
ｃｌｉｃｋｓｔｒ命令を使うと、特定の文字が来たときに自動でＡボタンを待つこともできます。

clickstr "。",1	;残り1行未満の時はページ送り

たとえば。こんなふうに。「_。」が来るたびに。Ａボタンを待ちます。
;"_"の後ろの文字はclickstrの効果を無視する

改行は、
スクリプト中の改行が
そのまま改行になります。
ただし空行にはならないので、/
;"/"の後ろの改行はメッセージで改行にならない（次の全角文字まで有効
空行をあけたい場合はｂｒ命令を使って下さい。

あとは！ｄや！ｗや！ｓの後ろに数字を書くと、!s20メッセージの表示を遅らせたりできます。!s0
;!dはＡボタン、→ボタンでスキップ可能
;!wは不可
;!sは通常時のウェイトを変更（NEW!!）

clickstr ""	;Ａボタン待ち指定をリセット

;=======================================
!d100
メッセージ関連で大事なのがウィンドウの設定。@
ｓｅｔｗｉｎ命令を使います。

;setwinを使うと現在表示中のメッセージはＡボタン待ちでページ送りされる
setwin 0
上からぶら下がる形にしたり、
setwin 1,1
行数を変えたり、
setwin ,,0
フォントを変えたり、
setwin ,,1,0
色を変えたり、
setwin ,8,,1
全画面に表示するようにしたりできます。@
これならいろんなタイプのノベルが作れるんじゃないかな〜？

setwin ,3

if $0=1 goto *分岐

メッセージ表示についてはこんなところかな。\
もう一回聞く？
sel "いい",*graphic,"聞く",*message1

;=======================================
*graphic

!d100
次はグラフィック表示について。\

*graphic1
setwin 1,3,1,1

背景はｂｇ命令で、白から黒までの４階調と、画像をしきつめることができます。\

set %0,0
*bgloop	;背景色変更ループ
bg %0
%0　@/
add %0,1
if %0<=3 goto *bgloop

bg "bg_sky"	;拡張子".pgd"の場合は省略可
画像@
といった感じで。\

;=======================================
!d100
立ち絵画像はｌｄ命令で表示、
ｃｌ命令で消去できます。\

set %0,0
*ldloop	;立ち絵位置変更ループ
cl	;位置無指定の場合は全消去
ld "pvc_rv",%0
if %0=0 左　@/
if %0=1 中央　@/
if %0=2 右@
add %0,1
if %0<=2 goto *ldloop

cl 2
ld "pvc_rv",1
の３ヶ所に表示できます。\

;=======================================
!d100
さらにＰ／ＶＮｓｃｒｉｐｔｅｒでは、スプライトと言って自由な位置に画像を表示できます！\

lsp "piece",0,96,16

ｌｓｐ／ｌｓｐｈ命令で登録、
ｖｓｐ命令で表示変更、
ｃｓｐ命令で登録解除します。\

vsp 0,,48,44

キャラの表情を変えるのに使ってみてもいいかもね。\

csp 0	;メモリをクリア

if $0=1 goto *分岐

グラフィック表示についてはこんなところかな。\
もう一回聞く？
sel "いい",*sound,"聞く",*graphic1

;=======================================
*sound

!d100
次は効果音、ＢＧＭについて。\

*sound1
setwin 1,3,1,1
bgmstop	;ＢＧＭを停止

効果音はｓｎｄ命令。@
snd "ohit"	;拡張子".ppd"の場合は省略可、ch無指定なら1ch
停止するときはｓｎｄｓｔｏｐ命令を使います。\
sndstop	;ch無指定なら全ch
メモリをクリアする効果もあるので、再生し終わったらｓｎｄｓｔｏｐ命令を必ず呼んで下さい。
最高で３ｃｈ同時に鳴らせます。@
メモリの都合上鳴らす人はあまりいないと思うけど〜。\

;=======================================
!d100
ＢＧＭはｂｇｍ命令。@
bgm "winnie"	;拡張子".pmd"の場合は省略可
停止するときはｂｇｍｓｔｏｐ命令を使います。\

if $0=1 goto *分岐

効果音、ＢＧＭについてはこんなところかな。\
もう一回聞く？
sel "いい",*other,"聞く",*sound1

;=======================================
*other

!d100
あとは変数とか選択肢とかぐらいかな〜。\

*other1
setwin 1,3,1,1

set %0,256
set %1,16
set %2,3
変数はローカル変数が％０〜％２５５の%0コ、グローバル変数が＄０〜＄１５の%1コ。\
ローカル変数は%2つあるセーブデータごとに中身が別々で、グローバル変数は全セーブデータ共通。\
;メッセージ中に変数の値を表示する場合は↑のようにします

グローバル変数は変更されると自動的にセーブされるので、クリアしたフラグに使ったりとか。\
;↑セーブデータを定義した場合

このチュートリアルも最後まで見たら、今度見たときはちょっと変わってるので要チェック！\

;=======================================
!d100

選択肢はｓｅｌ命令です。@
選んだ結果によってラベルにジャンプします。\

選択肢は最大でウィンドウの行数まで表示できるよ。@
簡単な例を出すね。\

*mondai
setwin ,8
set %0,0	;解答確認用
問題：１から１０まで全部足すといくつ？
sel "３５",*final,"４５",*final,"５５",*seikai,"６５",*final

*seikai
set %0,1	;正解フラグを立てる

*final
setwin ,3
ファイナルアンサー？
sel "ファイナルアンサー！",*hantei,"ちょっと待って",*mondai

*hantei
bgmstop
gosub *点々
snd "ohit"
br　　　　/
if %0=1 正　解　ッ　！\
if %0=0 残　念　ッ　！\

sndstop
bgm "winnie"
…とまぁ、みの○んた風にやってみましたがこんな感じです。\

if $0=1 goto *分岐

変数、選択肢についてはこんなところかな。\
もう一回聞く？
sel "いい",*ending,"聞く",*other1

;=======================================
*ending
!d100
setwin 1,3,1,1

以上、ノベルを作る方法について、簡単に紹介しました〜。\

cl
gosub *点々
お〜い、ラビルちゃ〜ん。\

ld "pvc_rv",0
ld "pvc_mp",2
ラビル「お、Ｐ子。何？」\
Ｐ子「今日はＰ／ＶＮＳｃｒｉｐｔｅｒのチュートリアルの収録があるって聞いたんだけど？」\
ラビル「ああ、大丈夫大丈夫。@
今テキトーに終わらせといたから安心したまえ。」\
Ｐ子「ええー、私の出番はー？」\
ラビル「それでは、みんなも面白い作品を作ってね〜♪」\

cl
ld "pvc_mp",1
gosub *点々
こら、待てー！！\

;=======================================
*staffroll

bg 3
cl
gosub *点々

Ｐ／ＶＮＳｃｒｉｐｔｅｒ
チュートリアル\

グラフィック
ＫＡＲＡＳＵ氏
ＢＡＫＡ　ＷＥＰＯＮ’Ｓ\

効果音
鼻クローン氏
ＣＫ　ＧＡＭＥ　ＦＡＣＴＯＲＹ\

ＢＧＭ
ＣＨＥＭＯＯＬ氏
爆竹オンライン\

Ｐｒｅｓｅｎｔｅｄ　ｂｙ
ヅラＣｈｕ
（ｃ）２００４　てとら★ぽっと\

set $0,1	;グローバル変数で最後まで見たフラグを立てる
end	;終了

;=======================================
*点々	;サブルーチン
・!d50
・!d50・!d50・!d50
・!d50・!d50・!d50・!d50・\
return

;=======================================
*分岐	;ラベルは日本語可
!d100
setwin ,8
何について紹介しようか。
sel "メッセージ表示",*message1,"グラフィック表示",*graphic1,"効果音、ＢＧＭ",*sound1,"変数、選択肢",*other1,"終了する",*ending

